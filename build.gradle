import com.google.gson.Gson
import net.fabricmc.loom.task.RemapJarTask

buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
	}
}

plugins {
	id "net.darkhax.curseforgegradle" version "1.0.7"
	alias(libs.plugins.quilt.loom)
	id 'maven-publish'
}

version = "${project.mod_version}+${libs.versions.minecraft.get()}"
archivesBaseName = project.modid
group = project.maven_group

repositories {}

dependencies {
	minecraft libs.minecraft
	mappings( loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
	})
	modImplementation libs.quilt.loader
	modImplementation libs.qsl
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/modrinth.gradle'
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 357304
	String changelogText = new File("CHANGELOG.md").getText('UTF-8')

	def mainFile = upload(projectId, remapJar)
	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "release"
	mainFile.displayName = "Structure Void Toggle v${project.mod_version} (Quilt)"
}

task buildAndRelease{}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.publishModrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.publishModrinth)